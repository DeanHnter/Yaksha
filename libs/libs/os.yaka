import libs.strings
import libs.strings.array as sarr
import libs.os.path as ospath

@nativedefine("struct yk__arguments*")
class arguments:
    # Number of arguments
    argc: int
    # Arguments themselves
    argv: Array[str]


# Get command line arguments
@nativedefine("yk__get_args")
def get_args() -> arguments:
    pass

# Get exe file path, or empty string on return
@native
def exe() -> str:
    ccode """int length = wai_getExecutablePath(NULL, 0, NULL);
    char* path = malloc(length + 1);
    if (path == NULL) return yk__sdsempty();
    wai_getExecutablePath(path, length, NULL);
    path[length] = '\\0';
    yk__sds value = yk__sdsnewlen(path, length);
    free(path);
    return value"""

# Get exe directory path, or empty string on return
@native
def exe_path() -> str:
    ccode """int length = wai_getExecutablePath(NULL, 0, NULL);
    char* path = malloc(length + 1);
    if (path == NULL) return yk__sdsempty();
    int path_len;
    wai_getExecutablePath(path, length, &path_len);
    path[path_len] = '\\0';
    yk__sds value = yk__sdsnewlen(path, path_len);
    free(path);
    return value"""

# Get current working directory, or empty string on return
@native
def cwd() -> str:
    ccode """char* path = yk__get_current_dir_path();
    if (path == NULL) return yk__sdsempty();
    yk__sds value = yk__sdsnewlen(path, strlen(path));
    free(path);
    return value"""


@nativedefine("struct yk__process_result*")
class ProcessResult:
    ok: bool
    output: str
    return_code: int


@native("yk__run")
def run(args: Array[str]) -> ProcessResult:
    pass

@native("yk__free_process_result")
def del_process_result(pr: ProcessResult) -> None:
    pass

# Are we running Windows?
@native
def is_windows() -> bool:
    ccode """bool win = false;
    #if defined(_WIN32) || defined(_WIN64)
    win = true;
    #endif
    return win"""

@native
def is_macos() -> bool:
    ccode """bool mach_os = false;
    #if defined(__APPLE__) && defined(__MACH__)
    mach_os = true;
    #endif
    return mach_os"""

# Get environment variable, empty if not found or error
@native("yk__getenv")
def getenv(name: str) -> str:
    pass


# Try to find full path to binary in PATH, no need .exe for windows
def which(binary: str) -> str:
    actual_bin: str = binary
    env: str = getenv("PATH")
    sep: str = ":"
    if is_windows():
        sep = ";"
        actual_bin = actual_bin + ".exe"
    paths: Array[str] = strings.split(env, sep)
    defer sarr.del_str_array(paths)
    length: int = len(paths)
    while length > 0:
        length = length - 1
        cur_path: str = paths[length]
        full_path:str = ospath.join(cur_path, actual_bin)
        if ospath.executable(full_path):
            #sarr.del_str_array(paths)
            return full_path
    # Not found from any of the paths, try fallbacks
    # Current path?
    special: str = ospath.join(cwd(), actual_bin)
    if ospath.executable(special):
        #sarr.del_str_array(paths)
        return special
    # Exe path?
    special = ospath.join(exe_path(), actual_bin)
    if ospath.executable(special):
        #sarr.del_str_array(paths)
        return special
    #sarr.del_str_array(paths)
    return ""
